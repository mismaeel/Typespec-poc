targetScope = 'resourceGroup'

param apimName string
param appInsightsName string

resource apiManagement 'Microsoft.ApiManagement/service@2020-12-01' existing = {
  name: apimName
}

resource apiManagementLogger 'Microsoft.ApiManagement/service/loggers@2020-12-01' existing = {
  name: '${apimName}/${appInsightsName}'
}

resource apimApiTypespec 'Microsoft.ApiManagement/service/apis@2020-12-01' = {
  name: 'widget-api-typespec'
  parent: apiManagement
  properties: {
    path: 'widget-typespec'
    apiRevision: '1'
    displayName: 'widget API Typespec'
    description: 'widget service is a sample service generated by GitHub CopTypespec.'
    subscriptionRequired: false
    format: 'openapi-link'
    value:  'https://raw.githubusercontent.com/pascalvanderheiden/ais-apim-copilot/main/deploy/release/openapi/openapi_copilot.json'
    protocols: [
      'https'
    ]
  }
}


resource apiTypespecPolicies 'Microsoft.ApiManagement/service/apis/policies@2020-12-01' = {
  name: 'policy'
  parent: apimApiTypespec
  properties: {
    value: loadTextContent('./policies/api_policy.xml')
    format: 'rawxml'
  }
}


resource apiMonitoringTypespec 'Microsoft.ApiManagement/service/apis/diagnostics@2020-06-01-preview' = {
  name: 'applicationinsights'
  parent: apimApiTypespec
  properties: {
    alwaysLog: 'allErrors'
    loggerId: apiManagementLogger.id  
    logClientIp: true
    httpCorrelationProtocol: 'W3C'
    verbosity: 'verbose'
    operationNameFormat: 'Url'
  }
}
